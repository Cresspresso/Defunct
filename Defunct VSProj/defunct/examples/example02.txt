/// Basics

def identity [x.x]
def true  [y x. y]
def false [y x. x]


/// CN Operators

def ++  [w. [y x. y(w y x)]]
def add [x y. y ++ x]
def mul [x y. [z.x(y z)]]

/// Church Numerals (CN)

def 0 false
def 1 (++ 0)
def 2 (++ 1)
def 3 (++ 2)
def 4 (++ 3)
def 5 (++ 4)
def 6 (++ 5)
def 7 (++ 6)
def 8 (++ 7)
def 9 (++ 8)
def 10 (++ 9)
def 11 (++ 10)
def 12 (++ 11)
def 13 (++ 12)
def 14 (++ 13)
def 15 (++ 14)

def 21 (add 8 13)
def 34 (add 13 21)
def 55 (add 21 34)


/// Pairs

def pair	[a b. [z. z a b]]
def pair_first	[p. p true]
def pair_second	[p. p false]


/// CN Operators

def --  [n. n (
	[p. [z. z (++ (pair_first p)) (pair_first p)]]
	) [z.z 0 0] false]

def sub [x y. y -- x]


/// Logical Operators

def and [x y. x y false]
def or  [x y. x true y]
def not [x. x false true]


/// CN Comparison Operators

def		  eq0 [x. x false not false]
def_u ==0 eq0
def		 ge	[x y. eq0 (x -- y)]
def_u >= ge
def		 eq	[x y. and (ge x y) (ge y x)]
def_u == eq
def		 ne	[x y. not (eq x y)]
def_u != ne
def		 gt	[x y. and (ge x y) (not (eq x y))]
def_u >  gt
def		 lt	[x y. not (ge x y)]
def_u <  lt
def		 le	[x y. or (lt x y) (eq x y)]
def_u <= le


/// Recursion

def Y [y.[$q.y(q q)][$q.y(q q)]]


/* sum1toN (n: CN) -> CN
 * examples:
 *	(sum1toN 3) -> 6
*/
def sum1toN (Y [r n.  eq0 n 0 (n ++ (r (-- n)))  ])

do (print_flush "sum1toN")
do (print_flush (sum1toN 3))
do (print_flush (sum1toN 4))
do (print_flush (sum1toN 5))


/// Fibonacci Sequence

def fibonacciYR (Y [r n.[a b.  (eq0 n) (b) (r (-- n) b (a ++ b))  ]])


/* fibonacci1 (a: CN, b: CN, n: CN) -> CN  where
 *	a: CN - Starting 1st term.
 *	b: CN - Starting 2nd term.
 *	n: CN - How many terms to evaluate. Returns the (n+1)th term of the series.
*/
def fibonacci1 [a b n.
	(eq0 n)
		(a)
		(fibonacciYR (-- n) a b)
]


/* fibonacci (n: CN) -> CN  where
 *	n: CN - How many terms to evaluate. Returns the (n+1)th term of the series.
 * examples:
 *	(fibonacci 4 0 1) -> 5
 *	(fibonacci n 0 1) for n >= 0:
 *	0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, ...
*/
def fibonacci [n. fibonacci1 0 1 n]

do (print_flush "fibonacci")
do (print_flush (fibonacci 0))
do (print_flush (fibonacci 1))
do (print_flush (fibonacci 2))
do (print_flush (fibonacci 3))
do (print_flush (fibonacci 4))
do (print_flush (fibonacci 5))
do (print_flush (fibonacci 6))


